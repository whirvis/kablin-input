apply plugin: 'java-library'

repositories {
    maven { url = uri('https://jitpack.io') }
}

publishing {
    publications {
        /*
         * JXInput is not on the Maven Central repository, meaning
         * it will not be found when trying to include it from this
         * dependency. Adding Jitpack to the POM automatically here
         * fixes this issue.
         */
        maven(MavenPublication) {
            pom.withXml {
                def repositoriesNode = asNode().appendNode('repositories')
                def jitpackNode = repositoriesNode.appendNode('repository')
                jitpackNode.appendNode('id', 'jitpack.io')
                jitpackNode.appendNode('url', 'https://jitpack.io')
            }
        }
    }
}

dependencies {
    api project(':devices:xbox')

    implementation 'com.github.strikerx3:jxinput:1.0.0'
}
