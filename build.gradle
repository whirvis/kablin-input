subprojects {
    /*
     * Some modules in this project are blank modules. They are simply folders
     * which contain for real modules. By default, Gradle will run tasks for
     * them, even though they have no source code. The closure below disables
     * this undesirable behavior.
     */
    def blankModules = ["adapter", "device"]
    if(blankModules.contains(project.name)) {
        tasks.whenTaskAdded { task -> task.enabled = false}
    }

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group 'io.ketill'
    version '0.0.1-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

	publishing {
		publications {
            maven(MavenPublication) {
                from components.java
            }
        }
	}

    repositories {
        mavenLocal()
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:22.0.0'

        api 'io.reactivex.rxjava3:rxjava:3.1.4'

        api 'org.joml:joml:1.10.2'

        testCompileOnly 'org.jetbrains:annotations:22.0.0'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
        testImplementation 'org.mockito:mockito-inline:4.3.1'
        testImplementation 'org.mockito:mockito-junit-jupiter:4.3.1'
        testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.10'

        if(project.name != "test") {
            testImplementation project(':test')
        }
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    archivesBaseName = "${rootProject.name}-${project.name}"
}
